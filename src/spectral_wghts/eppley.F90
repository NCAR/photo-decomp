! Copyright (C) 2020 National Center for Atmospheric Research
! SPDX-License-Identifier: Apache-2.0
!
!> \file
!> This eppley_spectral_wght module

!> The eppley type and related functions
module tuvx_spectral_wght_eppley

  use tuvx_spectral_wght,    only : spectral_wght_t
  use musica_constants,      only : dk => musica_dk

  implicit none

  private
  public :: spectral_wght_eppley_t

  !> Calculator for eppley_spectral_wght
  type, extends(spectral_wght_t) :: spectral_wght_eppley_t
  contains
    !> Calculate the spectral wght
    procedure :: calculate => run
  end type spectral_wght_eppley_t

  !> Constructor
  interface spectral_wght_eppley_t
    module procedure constructor
  end interface spectral_wght_eppley_t

contains

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  !> Initialize the spectral wght
  function constructor( config, grid_warehouse, profile_warehouse )           &
      result ( this )

    use musica_assert,                 only : assert_msg
    use musica_config,                 only : config_t
    use musica_string,                 only : string_t
    use tuvx_spectral_wght,            only : base_constructor
    use tuvx_grid_warehouse,           only : grid_warehouse_t
    use tuvx_profile_warehouse,        only : profile_warehouse_t

    !> Arguments
    class(spectral_wght_t),    pointer       :: this
    type(config_t),            intent(inout) :: config
    type(grid_warehouse_t),    intent(inout) :: grid_warehouse
    type(profile_warehouse_t), intent(inout) :: profile_warehouse

    !> Local variables
    character(len=*), parameter :: Iam = 'eppley constructor: '

    allocate( spectral_wght_eppley_t :: this )
    call base_constructor( this, config, grid_warehouse, profile_warehouse )

  end function constructor

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  !> Calculate the spectral weight
  function run( this, grid_warehouse, profile_warehouse ) result( spectral_wght )

    use musica_string,          only  :  string_t
    use tuvx_grid_warehouse,    only  :  grid_warehouse_t
    use tuvx_profile_warehouse, only  :  profile_warehouse_t
    use tuvx_grid,              only  :  grid_t

    !> Arguments
    class(spectral_wght_eppley_t), intent(in) :: this
    !> Warehouses
    type(grid_warehouse_t), intent(inout)              :: grid_warehouse
    type(profile_warehouse_t), intent(inout)           :: profile_warehouse
    !> Calculated spectral wght
    real(kind=dk), allocatable                         :: spectral_wght(:)

    !> Local variables
    real(dk), parameter         :: NINETY = 90.0_dk
    character(len=*), parameter :: Iam = 'eppley calculate: '
    real(dk)                    :: accum

    type(string_t)              :: Handle
    class(grid_t), pointer      :: lambdaGrid => null()

    Handle = 'wavelength' ; lambdaGrid => grid_warehouse%get_grid( "wavelength", "nm" )

    spectral_wght = this%spectral_wght_parms(1)%array(:,1)
    accum = dot_product( spectral_wght,lambdaGrid%delta_ )
    spectral_wght = NINETY*spectral_wght/accum

    if( associated( lambdaGrid ) ) deallocate( lambdaGrid )

  end function run

end module tuvx_spectral_wght_eppley
